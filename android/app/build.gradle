plugins {
    id "com.android.application"
    id "kotlin-android"
    // The Flutter Gradle Plugin must be applied after the Android and Kotlin Gradle plugins.
    id "dev.flutter.flutter-gradle-plugin"
}

def hasRelProp = { name -> project.hasProperty(name) && project.property(name)?.toString()?.trim() }
def hasReleaseKeystore =
        hasRelProp('RELEASE_STORE_FILE') &&
        hasRelProp('RELEASE_STORE_PASSWORD') &&
        hasRelProp('RELEASE_KEY_ALIAS') &&
        hasRelProp('RELEASE_KEY_PASSWORD')

android {
    namespace = "org.getlantern.lantern"
    compileSdk = 36
    ndkVersion "27.0.12077973"

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
            jniLibs.srcDirs += ['src/main/jniLibs']
        }
    }

    externalNativeBuild {
        cmake {
            path "cpp/CMakeLists.txt"
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = "17"
    }

    // Use legacy packaging to helps reduce apk size
    packagingOptions{
        exclude "DebugProbesKt.bin"
        jniLibs {
            useLegacyPackaging true
        }
    }

    defaultConfig {
        applicationId = "org.getlantern.lantern"
        minSdkVersion = 24
        targetSdk = 36
        versionCode = flutter.versionCode
        versionName = flutter.versionName

        ndk {
            // Specifies the ABI configurations of native libraries Gradle should build and package with the app.
            abiFilters = ["x86_64", "arm64-v8a"]
            debugSymbolLevel 'FULL'
        }

        externalNativeBuild {
            cmake {
                cppFlags "-std=c++14 -fexceptions -frtti"
                arguments "-DANDROID_ARM_NEON=TRUE",'-DANDROID_STL=c++_shared'
            }
        }
        buildFeatures {
            buildConfig true
        }
    }

    signingConfigs {
        if (hasReleaseKeystore) {
            release {
                storeFile file(project.property('RELEASE_STORE_FILE'))
                storePassword project.property('RELEASE_STORE_PASSWORD')
                keyAlias project.property('RELEASE_KEY_ALIAS')
                keyPassword project.property('RELEASE_KEY_PASSWORD')
            }
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            debuggable true
            signingConfig signingConfigs.debug
            buildConfigField "boolean", "DEVELOPMENT_MODE", "true"
        }

        release {
            minifyEnabled true
            shrinkResources true
            if (hasReleaseKeystore) {
                signingConfig signingConfigs.release
            } else {
                signingConfig signingConfigs.debug
            }
            buildConfigField "boolean", "DEVELOPMENT_MODE", "false"
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

}

flutter {
    source = "../.."
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
    implementation fileTree(dir: "libs", include: "liblantern.aar")
    implementation 'androidx.core:core-ktx:1.15.0'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.8.7'
    implementation 'androidx.work:work-runtime-ktx:2.10.0'
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.5'
}
