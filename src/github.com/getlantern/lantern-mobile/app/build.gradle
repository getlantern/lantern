apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

def incVersionNum() {
    def versionPropsFile = file('version.properties')
    def Properties versionProps = new Properties()

    // only try to load properties if version.properties
    // file exists
    if (versionPropsFile.exists()) {
       versionProps.load(new FileInputStream(versionPropsFile))
    }

    def value = 0
    def runTasks = gradle.startParameter.taskNames
    if ('assembleRelease' in runTasks) {
        value = 1
    }

    def code = (versionProps['VERSION_CODE'] ?: "0").toInteger() + value
    versionProps['VERSION_CODE'] = code.toString()
    versionProps.store(versionPropsFile.newWriter(), null)

    return code
}

android {

    useLibrary 'org.apache.http.legacy'

    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    def code = incVersionNum()

    defaultConfig {
	minSdkVersion rootProject.minSdkVersion
	targetSdkVersion rootProject.targetSdkVersion
	applicationId "org.getlantern.lantern"
	versionCode code
        versionName "1.0." + code
    }

    lintOptions {
        abortOnError false
    }

    signingConfigs {
        release {
            if (project.hasProperty("KEYSTORE_FILE")) {
                storeFile file(KEYSTORE_FILE)
                keyAlias "Lantern Release Key"
                storePassword KEYSTORE_PWD
                keyPassword KEY_PWD
            }
        }
    }

    buildTypes {

        debug {
            minifyEnabled false
        }

        release {
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro', '../../../../../MobileSDK/sdk/proguard-rules.pro'
        }

        applicationVariants.all { variant ->

            variant.buildConfigField "String", "LANTERN_VERSION", "\"$project.lanternVersion\""

            variant.outputs.each { output ->
                output.outputFile = new File(
                    output.outputFile.parent,
                    output.outputFile.name.replace("app-debug", "lantern-debug")
                )
            }
        }
    }
    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
    jcenter()
    mavenCentral()
    mavenLocal()
    maven { url 'https://maven.fabric.io/public' }
}

dependencies {
    compile(name:'liblantern', ext:'aar')
    compile(name:'sdk-debug', ext:'aar')
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile gplay
    compile appCompat
    compile 'com.google.guava:guava:19.0'
    compile 'com.mandrillapp.wrapper.lutung:lutung:0.0.6'
    compile 'com.github.tony19:timber-loggly:1.0.1'
    compile('com.crashlytics.sdk.android:crashlytics:2.5.3@aar') {
        transitive = true
    }
}
